<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="F_AdcPtpTime" Id="{f6adc510-bb78-47f6-8f17-20b940dee3b3}" SpecialFunc="None">
    <Declaration><![CDATA[(* Calculates the absolute time at which a Hall transition occurred. *)
FUNCTION F_AdcPtpTime : T_DCTIME64
VAR_INPUT
	hallId: INT;  (* The ID number of the Hall sensor that had the transition event. *)
	adcTime: UDINT; (* The 32-bit event timestamp retrieved from the ADC.*)
	motionStartTimes: ST_StartTimes;  (* The motion start times for the blade set that caused the Hall event. *)
END_VAR
VAR
	adcId: E_HallADC;  (* The ADC to which the Hall switch is connected. *)
	startTime: T_DCTIME64;  (* The motion start time based on the ADC DC clock. *)
	eventTime: T_DCTIME64;  (* The full 64-bit DC time of the Hall event, based on the ADC DC clock.*)
	elapsedNs: ULINT;  (* Nanoseconds between the start of motion and the event. *)
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[adcId := hallId / GVL.cHallOnAdc1;
startTime := motionStartTimes.adcDcTime[adcId];
// The ADC will have saved only the lower 32 bits of the DC time of the event.
// We have to reconstruct the upper 32 bits. Assume that the event occurred
// sometime between the last PLC cycle and the current one. If the saved
// time is less than or equal to the lower 32 bits of the ADC DC time
// for the previous cycle, then the upper 32 bits of the should
// come from the ADC DC time for the current cycle. Otherwise
// we use the upper 32 bits of the ADC DC time from the previous cycle.
// In essence we demand that time never goes backward.
IF adcTime <= (GVL.curAdcDcTime[adcId] AND 16#00000000_FFFFFFFF) THEN
	eventTime := (GVL.curAdcDcTime[adcId] AND 16#FFFFFFFF_00000000) OR adcTime;
ELSE
	eventTime := (GVL.prevAdcDcTime[adcId] AND 16#FFFFFFFF_00000000) OR adcTime;
END_IF
elapsedNs := eventTime - startTime;
F_AdcPtpTime := motionStartTimes.ptpTime + elapsedNs;
]]></ST>
    </Implementation>
    <LineIds Name="F_AdcPtpTime">
      <LineId Id="7" Count="0" />
      <LineId Id="23" Count="7" />
      <LineId Id="39" Count="0" />
      <LineId Id="31" Count="1" />
      <LineId Id="34" Count="1" />
      <LineId Id="33" Count="0" />
      <LineId Id="36" Count="1" />
      <LineId Id="19" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>