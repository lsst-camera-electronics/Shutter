<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="F_AdcExtTime" Id="{7c38fc38-e280-4189-a3ee-ea4adf34d5f3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION F_AdcExtTime : T_DCTIME64
VAR_INPUT
	hallId: DINT;  // The ID number of the Hall sensor that caused the event.
	eventTime: ULINT;  // The timestamp recorded by the ADC.
	taskStartExtTime: T_TaskStartTimes;  // Saved task start times for current and previous cycles.
	adcSysTime: T_AdcSysTimes;  // Saved ADC SysTimes for current and previous cycles.
END_VAR
VAR CONSTANT
	rolloverCorrection: ULINT := 16#00000001_00000000;
END_VAR
VAR
	adcId: DINT;  // GVL.Adc0 or Adc1.
	prevSysTime: ULINT;  // The previous cycle's value of SysTime for the right ADC.
	elapsedTime: ULINT;  // Time between the event and prevSysTime.
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Converts a 32-bit ADC timestamp into a 64-bit external timestamp in T_DCTIME64 format.

// Assume that the ADC timestamp was created sometime between the start of the previous cycle and this one.
// This implies that we drain all ADC timestamps every cycle.

// Find out how much time has passed on the ADC clock between the start of the
// previous cycle and the Hall event. Take into account a possible rollover
// of the ADC SysTime.
adcId := hallId / GVL.cFirstAdc1Hall;
prevSysTime := adcSysTime[GVL.cPreviousCycle][adcId];
IF eventTime <= prevSysTime THEN
	elapsedTime := (eventTime + rolloverCorrection) - prevSysTime;
ELSE
	elapsedTime := eventTime - prevSysTime;
END_IF

// Assume that the ADC clock and the PLC task clock are running at the same rate.
// Then the external time for the event is just the elapsed time on the ADC
// clock added to the external time at the start of the last cycle.
F_AdcExtTime := taskStartExtTime[GVL.cPreviousCycle] + elapsedTime;
]]></ST>
    </Implementation>
    <LineIds Name="F_AdcExtTime">
      <LineId Id="7" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="24" Count="2" />
      <LineId Id="28" Count="0" />
      <LineId Id="30" Count="1" />
      <LineId Id="34" Count="1" />
      <LineId Id="32" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="38" Count="3" />
    </LineIds>
  </POU>
</TcPlcObject>