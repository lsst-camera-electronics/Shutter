<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_StateMoving" Id="{fb05c940-3975-4fce-84e7-a3eb8bb470da}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_StateMoving
VAR_IN_OUT
	stAxis  : ARRAY [GVL.cXP .. GVL.cXM] OF ST_AxisGroup;
	bTopEntry	: BOOL;
	eCommand	: E_Commands;
END_VAR
VAR_INPUT
	stHomeAxis			: ST_HomeAxis;
	stMoveAxisAbsolute	: ST_MoveAxisAbsolute;
	stMoveAxisRelative	: ST_MoveAxisRelative;
END_VAR
VAR_OUTPUT
	bMotionDone : BOOL;
END_VAR
VAR
	//state-machine
	nCurrentState	: INT;
	nNextState		: INT;
	bLowerEntry 	: BOOL;
	//homing subroutine
	fbHoming	: FB_Homing;
	//move relative vars
	fTime, fSpeed, fAccel, fJerk : LREAL;

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bTopEntry = TRUE THEN
	bTopEntry := FALSE;
	stAxis[GVL.cXP].bBrake := GVL.cBrakeOff;
	stAxis[GVL.cXM].bBrake := GVL.cBrakeOff;

	IF eCommand = E_Commands.homeAxis THEN
		nNextState := 1000;
	ELSIF eCommand = E_Commands.moveAxisAbsolute THEN
		nNextState := 1010;
	ELSIF eCommand = E_Commands.moveAxisRelative THEN
		(* 
			equations  		(distance in mm, time in sec)
			Velocity 		= 2*distance/time
			Acceleration 	= 8*distance/time^2
			jerk 			= 32*distance/time^3
		*)
		fTime	:= TIME_TO_LREAL(stMoveAxisRelative.moveTime)/1000;
		fSpeed 	:= (2*stMoveAxisRelative.distance)/(fTime);
		fAccel 	:= (8*stMoveAxisRelative.distance)/(fTime*fTime);
		fJerk	:= (32*stMoveAxisRelative.distance)/(fTime*fTime*fTime);
		nNextState := 1020;
	END_IF
	eCommand := E_Commands.noCommand;	
END_IF	
IF nCurrentState <> nNextState THEN
	bLowerEntry := TRUE;
END_IF
nCurrentState := nNextState;

CASE nCurrentState OF
		
	1000://Homing routine
		fbHoming(stAxis:=stAxis, bTopEntry:=bLowerEntry, bMotionDone=>bMotionDone);
		IF bMotionDone THEN
			nNextState := 2000;
		END_IF
		
	1010://move absolute
		stAxis[stMoveAxisAbsolute.axisIndex].mcMoveAbs(Axis:=stAxis[stMoveAxisAbsolute.axisIndex].axisRef,
			Position:=stMoveAxisAbsolute.position, Velocity:=stMoveAxisAbsolute.speed, Execute:=TRUE);
		IF stAxis[stMoveAxisAbsolute.axisIndex].mcMoveAbs.Done THEN
			stAxis[stMoveAxisAbsolute.axisIndex].mcMoveAbs(Axis:=stAxis[stMoveAxisAbsolute.axisIndex].axisRef, Execute:=FALSE);
			bMotionDone := TRUE;
			nNextState := 2000;
		END_IF
		
	1020://move relative with different motion profile parameter
		stAxis[stMoveAxisRelative.axisIndex].mcMoveRel(Axis:=stAxis[stMoveAxisRelative.axisIndex].axisRef,
			Distance:=stMoveAxisRelative.distance, Velocity:=fSpeed, Acceleration:=fAccel, Jerk:=fJerk, Execute:=TRUE);
		IF stAxis[stMoveAxisRelative.axisIndex].mcMoveRel.Done THEN
			stAxis[stMoveAxisRelative.axisIndex].mcMoveRel(Axis:=stAxis[stMoveAxisRelative.axisIndex].axisRef, Execute:=FALSE);
			bMotionDone := TRUE;
			nNextState := 2000;
		END_IF
		
			
	2000:

	
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="FB_StateMoving">
      <LineId Id="31" Count="1" />
      <LineId Id="153" Count="1" />
      <LineId Id="81" Count="1" />
      <LineId Id="233" Count="0" />
      <LineId Id="86" Count="2" />
      <LineId Id="248" Count="4" />
      <LineId Id="247" Count="0" />
      <LineId Id="255" Count="0" />
      <LineId Id="246" Count="0" />
      <LineId Id="253" Count="1" />
      <LineId Id="89" Count="0" />
      <LineId Id="92" Count="1" />
      <LineId Id="34" Count="0" />
      <LineId Id="143" Count="1" />
      <LineId Id="140" Count="0" />
      <LineId Id="35" Count="2" />
      <LineId Id="67" Count="1" />
      <LineId Id="138" Count="0" />
      <LineId Id="145" Count="2" />
      <LineId Id="157" Count="0" />
      <LineId Id="151" Count="0" />
      <LineId Id="168" Count="0" />
      <LineId Id="170" Count="2" />
      <LineId Id="174" Count="1" />
      <LineId Id="173" Count="0" />
      <LineId Id="176" Count="5" />
      <LineId Id="183" Count="0" />
      <LineId Id="232" Count="0" />
      <LineId Id="182" Count="0" />
      <LineId Id="184" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>